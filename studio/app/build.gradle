apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion 23
    buildToolsVersion "$rootProject.buildToolsVersion"

    dataBinding {
        enabled = true
    }
    defaultConfig {
        applicationId "nl.renedegroot.android.opengpstracker.exporter"
        minSdkVersion 10
        targetSdkVersion 23
        versionCode 3
        versionName "0.0.3"
        buildConfigField "int", "BUILD_NUMBER", System.getenv("BUILD_NUMBER") as String ?: "0"
        buildConfigField "String", "GIT_COMMIT", "\"" + System.getenv("GIT_COMMIT") + "\"" as String ?: "\"Unknown\""
        archivesBaseName = "opengpstracker-exporter-$versionName"
    }

    if (System.getenv().containsKey("storeFile")) {
        apply from: 'signing.gradle'
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    kapt {
        generateStubs = true
    }

    lintOptions {
        abortOnError false
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile project(':log')
    compile project(':integration')
    compile "com.google.android.gms:play-services-drive:$rootProject.playServicesVersion"
    compile "org.jetbrains.kotlin:kotlin-stdlib:$rootProject.kotlinVersion"
    compile "com.android.support:support-annotations:$rootProject.supportLibraryVersion"
    compile "com.android.support:appcompat-v7:$rootProject.supportLibraryVersion"
    compile "com.android.support:design:$rootProject.supportLibraryVersion"
    testCompile "junit:junit:$rootProject.junitVersion"
    testCompile "org.mockito:mockito-core:$rootProject.mockitoVersion"
    kapt 'com.android.databinding:compiler:2.1.0-beta3'
}
